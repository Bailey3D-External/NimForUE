#Host guest (which will be renamed as plugin) and the game will be compiled from this file. Nue will use functions from here. 
#We may extrac the compilation option to another file since there are a lot of platforms. 

import std / [ options, os, osproc, parseopt, sequtils, strformat, strutils, sugar, tables, times ]
import buildscripts/[buildcommon, buildscripts, nimforueconfig, nimcachebuild]
import ../switches/switches
let config = getNimForUEConfig()



proc compileHost*() = 
#generateFFIGenFile(config) # this is currently generated by nue g, but it should be generated by host itself here!
  doAssert(fileExists("./src/hostnimforue/ffigen.nim"), "Please run: nue g")

  let buildFlags = @[buildSwitches, targetSwitches, hostPlatformSwitches].foldl(a & " " & b.join(" "), "")
  doAssert(execCmd(&"nim cpp {buildFlags} --header:NimForUEFFI.h --threads --tlsEmulation:off --app:lib --nomain --d:host --nimcache:.nimcache/host src/hostnimforue/hostnimforue.nim") == 0)
  # copy header
  let ffiHeaderSrc = ".nimcache/host/NimForUEFFI.h"
  let ffiHeaderDest = "NimHeaders/NimForUEFFI.h"
  copyFile(ffiHeaderSrc, ffiHeaderDest)
  log("Copied " & ffiHeaderSrc & " to " & ffiHeaderDest)

  # copy lib
  let libDir = "./Binaries/nim"
  let libDirUE = libDir / "ue"
  createDir(libDirUE)

  let hostLibName = "hostnimforue"
  let baseFullLibName = getFullLibName(hostLibName)
  let fileFullSrc = libDir/baseFullLibName
  let fileFullDst = libDirUE/baseFullLibName

  try:
    copyFile(fileFullSrc, fileFullDst)
  except OSError as e:
    when defined windows: # This will fail on windows if the host dll is in use.
      quit("Error copying to " & fileFullDst & ". " & e.msg, QuitFailure)

  log("Copied " & fileFullSrc & " to " & fileFullDst)

  when defined windows:
    let weakSymbolsLib = hostLibName & ".lib"
    copyFile(libDir/weakSymbolsLib, libDirUE/weakSymbolsLib)
  elif defined macosx: #needed for dllimport in ubt mac only
    let dst = "/usr/local/lib" / baseFullLibName
    copyFile(fileFullSrc, dst)
    log("Copied " & fileFullSrc & " to " & dst)




proc compileWinPCH*() = 
  let buildFlags = @[buildSwitches, targetSwitches, ueincludes, uesymbols, getPlatformSwitches(true, true)].foldl(a & " " & b.join(" "), "")
  #TODO Create a single cpp. No need to create a Nim program for this
  
  let iTest = "-I" & quotes(config.pluginDir / "NimHeaders")
  if execCmd(&"nim cpp {buildFlags} --genscript  --app:lib --nomain --nimcache:.nimcache/winpch src/nimforue/unreal/winpch.nim") != 0:
    quit("! Error: Could not compile winpch.")

  var pchCmd = r"vccexe.exe /c --platform:amd64 /nologo " & iTest & " " &
    vccPchCompileFlags(withDebug, withPch = true, createPch = true).join(" ") & " " & 
    getUEHeadersIncludePaths(config).foldl(a & " -I" & quotes(b), " ")

  let definitionsCppPath = config.pluginDir / ".nimcache/winpch/@mdefinitions.nim.cpp"
  # let definitionsCppPath = config.pluginDir / ".nimcache/winpch/tocompile.cpp"
  if fileExists(definitionsCppPath):
    pchCmd &= " " & definitionsCppPath
  else:
    quit("!Error: " & definitionsCppPath & " not found!")

  let curDir = getCurrentDir()
  pchCmd &= " " & debugFlags() #NEXT
  #echo pchCmd
  setCurrentDir(".nimcache/winpch")
  discard execCmd(pchCmd)
  setCurrentDir(curDir)


proc compilePlugin*() =
  generateFFIGenFile(config)
  let bindingPrefix =
    "-d:BindingPrefix=.nimcache/gencppbindingsmacos/@m..@snimforue@sunreal@sbindings@sexported@s"
  
  let buildFlags = @[buildSwitches, targetSwitches, ueincludes, uesymbols, pluginPlatformSwitches].foldl(a & " " & b.join(" "), "")
  echo pluginPlatformSwitches
  let compCmd = &"nim cpp {buildFlags} {bindingPrefix} --app:lib --nomain --d:genffi -d:withPCH --nimcache:.nimcache/guest src/nimforue.nim"
  doAssert(execCmd(compCmd)==0)
  
  copyNimForUELibToUEDir()


proc compileGenerateBindings*() = 
  let buildFlags = @[buildSwitches, targetSwitches, pluginPlatformSwitches, ueincludes, uesymbols].foldl(a & " " & b.join(" "), "")
  doAssert(execCmd(&"nim  cpp {buildFlags}  --noMain --compileOnly --header:UEGenBindings.h  --nimcache:.nimcache/gencppbindingsmacos src/codegen/maingencppbindings.nim") == 0)
  copyFile("./.nimcache/gencppbindingsmacos/UEGenBindings.h", "./NimHeaders/UEGenBindings.h")
